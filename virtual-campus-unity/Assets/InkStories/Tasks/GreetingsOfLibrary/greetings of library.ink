//mission name: greetings of library

#after: greetings of headmaster

#name: 图书馆的问候
#description: 图书馆可是大学期间必定会拜访的地方，因此也有必要了解一下。不过似乎图书馆前站着一个和你有相似想法的新生，你决定去认识一下……

=== func_start_of_mission ===
#without: func_talk_with_studentZBW
#collidetrigger: university_library_collider
#talk_immediately_after_collision
在走进图书馆之前，你看到了门口站着一个人，瘦瘦高高，在图书馆门口，脸上有些迷茫。
-
+n
他手里面拿着一沓新生手册，看起来和你一样，也是今天刚来报到的新生。
-
+n
回想起来，似乎这是你进入学校以来，第一个见到的学生，你决定过去打个招呼。
-
+n
->DONE

=== func_talk_with_studentZBW ===
#collidetrigger: student_ZBW_lib
……
*你好……
你好啊！你也是新生嘛……
-
*你好……我叫……
你好你好，我叫<color=blue>张博闻</color>！我也是刚来这里的新生，这个学校好大啊，我这第一天来都快绕晕了。
-
+n
话说，你刚刚看了吗，那边有个小亭子，风景可好了。
-
*……
还有寝室，设施也还算齐全，上床下桌，还有活动室。
-
*<color=\#808080>（啊，他出乎意料的话很多诶）</color>
还有那个学生活动中心，各种各样的活动室，钢琴房、舞蹈房……
-
*<color=\#808080>（啊，似乎停不下来了……）</color>
<color=\#808080>（张博闻似乎开始自顾自地不停说了起来……）</color>
-
+n
……总之总之，我也是刚刚才逛到这里，刚想来图书馆看一看。可是转念一想呢，又觉得得先去食堂看一看，正在犹豫呢，你刚好就出现了。
-
*所以你的意思是……
那见面也算是认识了，你能帮我个忙嘛……
-
*果然……
我这会想先去各个食堂转一转，你能帮我逛一下图书馆嘛，就是帮我了解了解<color=blue>图书馆的使用事项</color>什么的，等会咱们还在这里会合。如何？
-
*为什么要这样？
你了解完图书馆，等会给我讲讲就可以了。我都上大学了，前十八年可是学习学够了，可是不怎么想再来图书馆。
-
+n
大学嘛……就是要玩个够！
-
*<color=\#808080>（好标准的大学新生发言）</color>
等会咱们各自完事了，就还来这里集合，我再带你走一遍学校各个食堂，你在路上再给我讲图书馆的事吧，交换情报，如何？
-
*也好……
那你快去吧，我先去看看食堂咯。饿死了饿死了……
-
*我也先去找图书馆前台了……
#disable: student_ZBW_lib
#enableNPC: lib_receptionist
#upd_description: 和张博闻交谈过后，你们决定分开行动：他去了解学校里的食堂；而你去了解有关图书馆的事情。快去找前面的图书馆管理员对话了解一下吧！
->DONE

=== func_talk_with_lib_recp ===
#override
#collidetrigger:lib_receptionist
同学你好，欢迎来到香港中文大学（深圳）大学图书馆。
* 你好，我是刚来的新生
新同学你好，有什么需要帮助的吗。
-
*能为我讲解一些图书馆的注意事项嘛
当然可以。那么，那么你想要了解哪方面的信息呢？
->introduction

=== func_return_to_studentLL ===
#collidetrigger: student_ZBW_lib
#require: func_talk_with_lib_recp
怎么样，打听完了吗？
*打听完了
这样嘛，刚好我也刚刚逛完所有食堂回来。
-
*那要大概听一下嘛
走吧走吧，时间也差不多了，咱们可以路上慢慢讲。
-
+n
现在，咱们也该去整个学校最神圣、最纯粹、最彰显学校内涵的场所了……
-
*嗯？不是该去参观食堂了吗？
嗯？就是食堂啊，难道我刚刚说的不是在形容食堂吗？
-
*……
走吧走吧，还在那站着干什么？我先去<color=red>学生活动中心一楼</color>的<color=red>潘多拉食堂</color>那里等你咯。
-
*潘多拉见
#endstory
->END



=== introduction ===
* 开馆时间 -> introduction.lib_time
* 馆藏资源 -> introduction.lib_resource
* 读者服务 -> introduction.lib_service
* 教室使用规定 -> introduction.lib_room_rule
* ->
    ++n
    没有其他问题了吗。那么如果对我校图书馆有其他问题的话，可以浏览图书馆官网以获取更多更详细的信息。
    -
    **谢谢你的介绍
    #upd_description: 你已经和图书馆管理员了解到了足够多的消息！那么，现在可以回到刚刚的地方去找张博闻了，按照约定，他会带你去游览学校的各个食堂。
    #enable: student_ZBW_lib
    #addskin: 奔跑者
    ->DONE

= lib_time
+n
大学图书馆的工作时间是 <color=blue>8:30-23:30</color>（周一至周五） <color=blue>8:30-23:30</color>（周末）
-
+n
启动区图书馆的工作时间是 <color=blue>7:30-23:30</color>（周一至周五） <color=blue>7:30-23:30</color>（周末）
-
+n
根据校历安排，以上服务时间后期可能调整。另，节假日请留意通知提醒。
-
->introduction

= lib_resource
+n
截止目前 2019 年，图书馆馆藏总数达 132,758 册。其中外文 63,221 册，中文 69,573册；电子图书总数超过 248 万种；多媒体资料 2,198 种；数据库（期刊、数值数据、古籍等）105 个。
-
->introduction

= lib_service
+n
根据香港中文大学（深圳）的有关规定，图书馆借书证即为读者所持有的本人<color=blue>校园卡</color>。读者可凭卡使用图书馆内的相关资源。
-
+n
如校园卡丢失，读者可在<color=blue>校园卡中心</color>挂失并办理补办手续。校园卡中心位于<color=blue>学生活动中心及教学楼 A 之间的下沉区域</color>。
-
+n
本馆提供四种可供外借的资源：普通馆藏、DVD、通识书籍(GE)、教参(CR)。
-
+n 
对于学生来说，这四种资源的借阅总数<color=blue>不能超过10个</color>。
-
+n
每种资源都有借阅期限，学生一次借阅后，须在借阅期限内归还或提出续借，续借后再次刷新借阅期限，但借阅总时间不能超出最长借阅期限。
-
+n
普通馆藏书籍的借阅期限是<color=blue>28天</color>，最长借阅期限是<color=blue>112天</color>。DVD的借阅期限是7天，最长借阅期限是7天。通识书籍的借阅期限是28天，最长借阅期限是112天。教参的借阅期限是2小时，最长借阅期限是4小时。
-
+n
一旦超期，除了教参以外，其他三种资源的超期罚款均为￥1/天；而教参的超期罚款为￥1/小时。
-
+n
图书馆还有一类特殊资源：校长藏书，此类书籍一次仅能借阅一本，借阅期限仅有三天，但是没有最长借阅期限。
-
->introduction

= lib_room_rule
+n
对于我校学生来说，旧图书馆及新图书馆一到四楼的所有自习室以及新图书馆二楼的静音舱都是开放的。
-
+n
自习室本科生和交换生每人每天最多预约<color=blue>2次</color>，每次最多<color=blue>2小时</color>；硕士生每人每天最多<color=blue>2次</color>，每次最多<color=blue>3小时</color>。
-
+n
静音舱每人每天最多<color=blue>2小时</color>。
-
+n
对于研究生来说，我校还额外开放新馆二楼的多人静音舱。每人每天最多<color=blue>2小时</color>。
-
+n
所有图书馆相关的房间预约均可统一在登录用户之后在<color=blue>我校图书馆官网</color>订阅。可在学校官网主页右上角找到图书馆官网。
-
+n
预约为组预约方式，设<color=blue>一名组长和多名组员</color>。系统将首位预约人设置为组长。组长预约的时候必须输入其他组员的统一账户账号才能完成预约。预约完成后所有组员的校园卡均可以打开小组学习室的房门；
-
+n
预约生效以后，所有组员均需在预约时间开始后 <color=blue>10 分钟</color>内刷卡进入，如有一名或若干名组员未按规定刷卡使用，视为违规一次，由组长承担后果。如 10 分钟内无一人刷卡，则自动取消该预约。
-
+n 
违约责任系统记录在组长身上，违约累计达<color=blue>三次</color>者，系统将暂停其随后 30 天内所有研讨室的预约权限；违约记录累积时限为 1 个学年，未满三次的违约记录将于每年的 9 月 2 日清零。
-
->introduction
